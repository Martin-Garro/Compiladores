package codigo;
import java_cup.runtime.*;


   
action code {:
    
    public void report_error(String message, Object info) {
   
        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuffer m = new StringBuffer("Error");
   
        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuffer error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuffer error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }
   
        /* Add to the end of the StringBuffer error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuffer 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }
   
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(0);  // 1 return trace exception
    }
    
    TablaSimbolo tabla= new TablaSimbolo();
:};
   

   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   

terminal SEMI, PLUS, MINUS, TIMES, DIVIDE, LPAREN, RPAREN;
terminal EQUAL, INT, BOOL, RETURN, TRUE, FALSE;


terminal String ID;
terminal Integer NUMBER;

non terminal Arbol program, declaraciones, sentencias, declaracion, sentencia;
non terminal Tipos tipo;
non terminal Arbol expr, const;
   

/* -------------Precedence and Associatively of Terminals Section----------- */
   
/*
  Precedence of non terminals could be defined here.  If you do define
  precedence here you won't need to worry about precedence in the
  Grammar Section, i.e. that TIMES should have a higher precedence
  than PLUS.
  
  The precedence defined here would look something like this where the
  lower line always will have higher precedence than the line before it.
*/   
  precedence left PLUS, MINUS; 
  precedence left TIMES, DIVIDE; 



/* ----------------------------Grammar Section-------------------- */
   
 program ::= declaraciones:d sentencias:s {: RESULT = new Arbol(new Simbolo(Etiqueta.PROGRAMA),d,s); :}
             ;
             
 declaraciones ::= declaracion:d declaraciones:ds {: RESULT = new Arbol(new Simbolo(Etiqueta.DECLS),d,ds); :}
                  | declaracion:d                 {: RESULT = d; :}
                  ;
                  
  declaracion ::=   tipo:t ID:i EQUAL:a expr:e SEMI {: Simbolo simbolo = new Simbolo(Etiqueta.VARIABLE,i,t,0,ileft); 
                                                       if (tabla.insertar(simbolo)==null){ 
                                                           report_fatal_error("Variable redeclarada: "+simbolo.getNombre()+" (Línea "+simbolo.getNroLinea()+").\n",null);
                                                       } 
                                                       else{
                                                            RESULT = new Arbol(new Simbolo(Etiqueta.DECLARACION),new Arbol(simbolo),e);  
                                                       }   
                                                    :}
                  ;
                   
  tipo ::= INT   {: RESULT = Tipos.INT; :}
          | BOOL {: RESULT = Tipos.BOOL; :}
          ;         
  
  sentencias ::=  sentencia:s sentencias:ss {: RESULT = new Arbol(new Simbolo(Etiqueta.SENTS),s,ss); :}
                 | sentencia:s             {: RESULT = s; :}
                 ;
                 
  sentencia ::= RETURN:r expr:e SEMI       {: RESULT = new Arbol(new Simbolo(Etiqueta.RETURN),null,e); :}
                | ID:i EQUAL:a expr:e SEMI {: Simbolo simbolo = tabla.buscar(i); 
                                              if (simbolo==null){ 
                                                 report_fatal_error("Variable no declarada: "+i+" (Línea "+ileft+").\n",null);
                                              } 
                                              else{
                                                 RESULT = new Arbol(new Simbolo(Etiqueta.ASIGNACION),new Arbol(simbolo),e);  
                                              }  
                                           :}
                ;
                          
   expr      ::= expr:e1 PLUS:o expr:e2
                 {: RESULT = new Arbol(new Simbolo(Etiqueta.SUMA,"",Tipos.NOTTYPE,0,oleft),e1,e2); :}
                 |
                 expr:e1 MINUS:o expr:e2
                 {: RESULT = new Arbol(new Simbolo(Etiqueta.RESTA,"",Tipos.NOTTYPE,0,oleft),e1,e2); :}
                 |
                 expr:e1 TIMES:o expr:e2
                 {: RESULT = new Arbol(new Simbolo(Etiqueta.MULT,"",Tipos.NOTTYPE,0,oleft),e1,e2); :}
                 |
                 expr:e1 DIVIDE:o expr:e2
                 {: RESULT = new Arbol(new Simbolo(Etiqueta.DIV,"",Tipos.NOTTYPE,0,oleft),e1,e2); :}
                 |
                 LPAREN expr:e RPAREN 
                 {: RESULT = e; :}
                 |
                 const:c
                 {:  RESULT = c; :}
                 |
                 ID:i
                 {: Simbolo simbolo = tabla.buscar(i); 
                    if (simbolo==null){ 
                       report_fatal_error("Variable no declarada: "+i+" (Línea "+ileft+").\n",null);
                    } 
                    else{
                       RESULT = new Arbol(simbolo);  
                    }  
                    
                 :}
                 ;
                 
   const ::=     NUMBER:n
                 {: RESULT = new Arbol(new Simbolo(Etiqueta.VALORENTERO,"",Tipos.INT,n.intValue(),nleft)); :}
                 |   
                 TRUE:t
                 {: RESULT = new Arbol(new Simbolo(Etiqueta.VALORBOOLEANO,"",Tipos.BOOL,1,tleft)); :}
                 | 
                 FALSE:f              
                 {: RESULT = new Arbol(new Simbolo(Etiqueta.VALORBOOLEANO,"",Tipos.BOOL,0,fleft)); :}
                 ;
